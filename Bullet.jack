/** Implements a bullet; a simple class
  * to move and track bullet location */
class Bullet 
{
    static int WIDTH, HEIGHT, BULLET_SPEED;
    field int x, y; // x, y coordinates
    field int location; // memory address location
    field Array pixels; // All pixels of the sprite

    /** Initialize the Bullet class */
    function void init()
    {
        let WIDTH = 2;
        let HEIGHT = 4;
        let BULLET_SPEED = 2;
        return;
    }

    /** Class-level accessors */
    function int getWidth() { return WIDTH; }
    function int getHeight() { return HEIGHT; }

    /** Instantiates a bullet at x,y coordinates */
    constructor Bullet new(int Ax, int Ay)
    {
        let x = Ax;
        let y = Ay;
        let location = (32 * y) + (x / 16);
        do initPixels();
        return this;
    }

    /** Deallocates memory for the bullet */
    method void dispose()
    {
        do Memory.deAlloc(this);
        return;
    }

    /** Instance-level accessors */
    method int getX() { return x; }
    method int getY() { return y; }
    method int getLocation() { return location; }
    method Array getPixels() { return pixels; }

    /** Initializes the pixels array */
    method void initPixels()
    {
        let pixels = Array.new(12);
        let pixels[0] = 384;
        let pixels[1] = 384;
        let pixels[2] = 384;
        let pixels[3] = 384;
        // The rest should be automatically initialized to 0

        return;
    }

    /** Updates the memory address location */
    method void updateLocation()
    {
        let location = (32 * y) + (x / 16);
        return;
    }

    /** Moves this bullet straight up */
    method void move()
    {
        let y = y - BULLET_SPEED;
        do updateLocation();
        return;
    }
}