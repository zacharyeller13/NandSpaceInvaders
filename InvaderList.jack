/** Implement a singly linked list of invaders.
  * There's probably some jank way I could figure out to make a generic list
  * But I haven't tried to work through that yet, so this will work for now */
class InvaderList 
{
    field Invader data; // current Invader
    field InvaderList next; // next Node

    /** Creates a list */
    constructor InvaderList new(Invader car, InvaderList cdr)
    {
        let data = car;
        let next = cdr;
        return this;
    }

    /** Disposes list and all items in the list */
    method void dispose()
    {
        if (~(next = null))
        {
            do next.dispose();
        }
        do Memory.deAlloc(this);
        return;
    }

    /** Accessors */
    method Invader getData() { return data; }
    method InvaderList getNext() { return next; }
    method void setNext(InvaderList nextList) { let next = nextList; return; }

    /** Move all invaders in list */
    method void move()
    {
        var InvaderList current;
        var Invader currentInvader;
        let current = this;
        while (~(current = null))
        {
            let currentInvader = current.getData();
            // If this is an empty list, currentInvader will be null
            if (~(currentInvader = null))
            {
                do currentInvader.move();
            }
            let current = current.getNext();
        }
        return;
    }

    /** Add an invader to the list
      * Typically would call only on the head node 
      * Not really best practice to always add as the 2nd node in the list
      * But it's faster and easier than iterating the whole list and adding to the end */
    method void add(Invader newInvader)
    {
        var InvaderList temp;

        // If an empty list, make it the first node
        if (data = null)
        {
            let data = newInvader;
            return;
        }

        let temp = InvaderList.new(newInvader, next);
        let next = temp;
        return;
    }

    /** Remove an invader from the list 
      * should always be called on the head node */
    method void remove(InvaderList invaderToRemove)
    {
        var InvaderList previous;
        var InvaderList current;
        var Invader currentData;

        // Base case on the head node
        if (this = invaderToRemove)
        {
            if (~(next = null))
            {
                do data.dispose();
                let data = next.getData();
                let previous = next;
                let next = next.getNext();

                // Prevent a memory leak by disposing of the node that's moved up
                do previous.setNext(null);
                do previous.dispose();
            }
            else
            {
                do data.dispose();
                let data = null;
            }
            return;
        }

        // Iterating through list to check each node
        let previous = this;
        let current = next;
        while (~(current = null))
        {
            if (current = invaderToRemove)
            {
                do previous.setNext(current.getNext());
                do current.setNext(null);
                let currentData = current.getData();
                do currentData.dispose();
                do current.dispose();
                return;
            }
            let previous = current;
            let current = current.getNext();
        }
        // Should never reach here, but if, for some reason, the invaderToRemove is orphaned
        // we must still return.
        return;

    }
    
    /** Visualize any node in the list */
    method void printNode()
    {
        if (data = null)
        {
            do Output.printString("null");
            return;
        }
        do Output.printInt(data.getX());
        do Output.printString(",");
        do Output.printInt(data.getY());
        do Output.println();
        return;
    }

    /** Visualize the entire list */
    method void printList()
    {
        do Output.printString("( data= ");
        do printNode();
        do Output.printString(" next= ");
        if (~(next = null))
        {
            do next.printList();
        }
        else
        {
            do Output.printString(" null ");
        }
        do Output.printString(" )");
        do Output.println();
        return;
    }
}