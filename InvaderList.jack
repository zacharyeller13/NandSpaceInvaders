/** Implement a singly linked list of invaders */
class InvaderList 
{
    field Invader data; // current Invader
    field InvaderList next; // next Node

    /** Creates a list */
    constructor InvaderList new(Invader car, InvaderList cdr)
    {
        let data = car;
        let next = cdr;
        return this;
    }

    /** Disposes list and all items in the list */
    method void dispose()
    {
        if (~(next = null))
        {
            do next.dispose();
        }
        do Memory.deAlloc(this);
        return;
    }

    /** Accessors */
    method Invader getData() { return data; }
    method InvaderList getNext() { return next; }
    method void setNext(InvaderList nextList) { let next = nextList; return; }

    /** Move all invaders in list */
    method void move()
    {
        var InvaderList current;
        var Invader currentInvader;
        let current = this;
        while (~(current = null))
        {
            let currentInvader = current.getData();
            do currentInvader.move();
            let current = current.getNext();
        }
        return;
    }

    /** Remove an invader from the list 
      * should always be called on the head node */
    method void remove(InvaderList invaderToRemove)
    {
        var InvaderList previous;
        var InvaderList current;
        var Invader currentData;

        // Base case on the head node
        if (this = invaderToRemove)
        {
            let next = null;
            do data.dispose();
            do dispose();
            return;
        }

        // Iterating through list to check each node
        let previous = this;
        let current = next;
        while (~(current = null))
        {
            if (current = invaderToRemove)
            {
                do previous.setNext(current.getNext());
                do current.setNext(null);
                let currentData = current.getData();
                do currentData.dispose();
                do current.dispose();
                return;
            }
            let previous = current;
            let current = current.getNext();
        }
        // Should never reach here, but if, for some reason, the invaderToRemove is orphaned
        // we must still return.
        return;
    }

}