/** Implements collision checking logic */
class CollisionChecker
{
    /** Check 1 bullet against all invaders 
      * And remove it if collision found
      * Return true if bullet was removed so that calling function can
      * Know to continue iterating or re-check */
    function boolean checkBulletCollisions(BulletList bulletToCheck, BulletList bullets, InvaderList invaders)
    {
        var Invader currentInvader;
        var Bullet bulletData;
        var InvaderList invadersHead;

        let bulletData = bulletToCheck.getData();
        let invadersHead = invaders;

        // Return early if this is an empty list
        if (bulletData = null)
        {
            return false;
        }

        while (~(invaders = null))
        {
            let currentInvader = invaders.getData();
            if (currentInvader = null)
            {
                return false;
            }

            if (CollisionChecker.checkCollision(bulletData, currentInvader))
            {
                do bullets.remove(bulletToCheck);
                do invadersHead.remove(invaders);
                return true;
            }

            let invaders = invaders.getNext();
        }
        return false;
    }

    /** Check a collision between 1 bullet and 1 invader 
      * using simple bounding box collision as I understand it 
      * see Intelligent 2D Collision and Pixel Perfect Precision on gamedev.net */
    function boolean checkCollision(Bullet bullet, Invader invader)
    {
        // Set up bounding boxes
        var int bulletTop, bulletBottom, bulletLeft, bulletRight;
        var int invaderTop, invaderBottom, invaderLeft, invaderRight;
        let bulletTop = bullet.getY();
        let bulletLeft = bullet.getX();
        let bulletBottom = bullet.getY() + bullet.getHeight();
        let bulletRight = bullet.getX() + bullet.getWidth();
        let invaderTop = invader.getY();
        let invaderLeft = invader.getX();
        let invaderBottom = invader.getY() + invader.getHeight();
        let invaderRight = invader.getX() + invader.getWidth();
        
        // Check horizontal collision
        if ((bulletLeft > invaderRight)
            | (invaderLeft > bulletRight))
        {
            return false;
        }

        // Check vertical collision
        // Less than since 0,0 starts in top right corner
        if ((bulletBottom < invaderTop)
            | (invaderBottom < bulletTop))
        {
            return false;
        }
        return true;
    }

    /** Check invader collisions with the floor/bottom of the screen
      * by accessing invader.landed */
    function boolean checkInvadersLanded(InvaderList invaders)
    {
        var Invader currentInvader;

        while (~(invaders = null))
        {
            let currentInvader = invaders.getData();
            if (currentInvader = null)
            {
                return false;
            }

            if (currentInvader.landed())
            {
                return true;
            }

            let invaders = invaders.getNext();

        }
        return false;
    }
}