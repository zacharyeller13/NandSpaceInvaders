/** Implement a singly linked list of bullets */
class BulletList
{
    field Bullet data;
    field BulletList next;

    /** Constructor the the list of bullets */
    constructor BulletList new(Bullet car, BulletList cdr)
    {
        let data = car;
        let next = cdr;
        return this;
    }

    /** Disposes list and all items in the list */
    method void dispose()
    {
        if (~(next = null))
        {
            do next.dispose();
        }
        do Memory.deAlloc(this);
        return;
    }

    /** Accessors */
    method Bullet getData() { return data; }
    method BulletList getNext() { return next; }
    method void setNext(BulletList nextList) { let next = nextList; return; }

    /** Move all bullets in the list; disposing of them when they exit the screen */
    method void move()
    {
        var BulletList current;
        var Bullet currentBullet;
        var BulletList previous;
        var BulletList temp;
        var BulletList head;

        let head = this;
        let current = this;
        
        while (~(current = null))
        {
            let currentBullet = current.getData();
            do currentBullet.move();

            // Exiting screen - dispose and shift list up
            if (currentBullet.getY() < (-Bullet.getHeight()))
            {
                let temp = current.getNext();
                do head.remove(current);
                let current = temp;
            }
            else
            {
                let previous = current;
                let current = current.getNext();
            }

        }
        return;
    }
    
    /** Remove bullet from the list 
      * should always be called on the head node */
    method void remove(BulletList bulletToRemove)
    {
        var BulletList previous;
        var BulletList current;
        var Bullet currentData;

        // Base case on head node
        if (this = bulletToRemove)
        {
            let next = null;
            do data.dispose();
            do dispose();
            return;
        }

        // Iterating through bullets to check each node
        let previous = this;
        let current = next;
        while (~(current = null))
        {
            if (current = bulletToRemove)
            {
                do previous.setNext(current.getNext());
                do current.setNext(null);
                let currentData = current.getData();
                do currentData.dispose();
                do current.dispose();
                return;
            }
            let previous = current;
            let current = current.getNext();
        }
        // Should never reach here, but if, for some reason, the bulletToRemove is orphaned
        // we must still return.
        return;
    }
}