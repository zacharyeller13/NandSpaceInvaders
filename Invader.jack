/** Implements an invader */
class Invader 
{
    static int WIDTH, HEIGHT; // sprite width and height constants
    field int x, y; // screen coordinates of the invader
    field int direction; // 1 or -1 for right vs left
    field int location; // mapped memory location of x,y coordinates

    /** Initializes the Invader class for static vars */
    function void init() 
    {
        let WIDTH = 16;
        let HEIGHT = 9;
        return;
    }

    /** Constructs a new invader with the given location */
    constructor Invader new(int Ax, int Ay) 
    {
        let x = Ax;
        let y = Ay;
        let direction = 1;
        let location = (32 * y) + (x / 16);
        do draw();
        return this;
    }

    /** Disposes the Invader */
    method void dispose() 
    {
        do Memory.deAlloc(this);
        return;
    }

    /** Draws this invader on the screen */
    method void draw() 
    {
        var int memAddress;
        let memAddress = 16384+location;
        do Memory.poke(memAddress+0, 2016);
        do Memory.poke(memAddress+32, 2064);
        do Memory.poke(memAddress+64, 4104);
        do Memory.poke(memAddress+96, 4104);
        do Memory.poke(memAddress+128, 32766);
        do Memory.poke(memAddress+160, -32767);
        do Memory.poke(memAddress+192, -32767);
        do Memory.poke(memAddress+224, -32767);
        do Memory.poke(memAddress+256, 32766);
        // We only need the first 9 rows, not a full 16
        return;
    }

    /** Erases invader from screen
      * used in both destroying and moving */
    method void erase() 
    {
        var int memAddress;
        let memAddress = 16384+location;
        do Memory.poke(memAddress+0, 0);
        do Memory.poke(memAddress+32, 0);
        do Memory.poke(memAddress+64, 0);
        do Memory.poke(memAddress+96, 0);
        do Memory.poke(memAddress+128, 0);
        do Memory.poke(memAddress+160, 0);
        do Memory.poke(memAddress+192, 0);
        do Memory.poke(memAddress+224, 0);
        do Memory.poke(memAddress+256, 0);
        return;
    }

    /** Erases invader from the screen and deallocates */
    method void destroy() 
    {
        do erase();
        do dispose();
        return;
    }

    /** Moves invader down at an angle */
    method void move() 
    {
        // Check that has not reached the bottom of the screen
        if (y < (255 - HEIGHT)) {
            do erase();
            do checkWallCollision();

            let x = x + (WIDTH * direction);
            let y = y + 2;
            let location = (32 * y) + (x / 16);
            do draw();
        }
        return;
    }

    /** Checks for collision on walls 
      * changing direction when necessary */
    method void checkWallCollision() 
    {
        if (direction = 1) 
        {
            if ((x + WIDTH) > 511) 
            {
                do changeDirection();
            }
        }
        else 
        {
            if ((x - WIDTH) < 0) 
            {
                do changeDirection();
            }
        }
        return;
    }
    
    /** Change direction if hits wall */
    method void changeDirection() 
    {
        let direction = direction * -1;
        return;
    }

    /** Accessor for x */
    method int getX()
    {
        return x;
    }

    /** Accessor for y */
    method int getY() 
    {
        return y;
    }
}