class Renderer
{
    /** Render all parts */
    function void renderState(Tank tank, BulletList bullets, InvaderList invaders)
    {
        var BulletList currentBullets;
        var Bullet currentBullet;
        var InvaderList currentInvaders;
        var Invader currentInvader;

        let currentBullets = bullets;
        let currentInvaders = invaders;

        do Screen.clearScreen();

        do Renderer.renderObject(tank.getLocation(), tank.getPixels());

        while (~(currentBullets = null))
        {
            let currentBullet = currentBullets.getData();
            do Renderer.renderObject(currentBullet.getLocation(), currentBullet.getPixels());
            let currentBullets = currentBullets.getNext();
        }

        while (~(currentInvaders = null))
        {
            let currentInvader = currentInvaders.getData();
            do Renderer.renderObject(currentInvader.getLocation(), currentInvader.getPixels());
            let currentInvaders = currentInvaders.getNext();
        }
        
        return;
    }

    /** Render any object by passing in its memory location and its sprite as an array of pixels */
    function void renderObject(int objectMemLocation, Array pixels)
    {
        var int memAddress;
        var int i;
        let memAddress = 16384+objectMemLocation;
        let i = 0;

        while (i < 12)
        {
            if (~(pixels[i] = 0))
            {
                do Memory.poke(memAddress+(i * 32), pixels[i]);
            }
            let i = i + 1;
        }
        return;
    }

    /** Clear the screen and render a game-over message */
    function void renderGameOver()
    {
        do Screen.clearScreen();
        do Output.printString("GAME OVER");
        return;
    }
}