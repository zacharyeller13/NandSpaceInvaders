/** Implements a Tank, the playable unit */
class Tank
{
    static int WIDTH, HEIGHT; // sprite width and height
    field int x, y; // screen coordinates of the tank
    field int direction; // -1, 0, or 1 for right, left, at rest
    field int location; // mapped memory location of x,y coordinates

    /** Constructs a new Tank */
    constructor Tank new() {
        let WIDTH = 16;
        let HEIGHT = 12;
        let x = 511 / 2;
        let y = 255 - HEIGHT;
        let direction = 0;
        let location = (32 * y) + (x / 16);
        do draw();
        return this;
    }

    /** Disposed the Tank from memory */
    method void dispose()
    {
        do Memory.deAlloc(this);
        return;
    }

    /** Draws Tank at its current screen coordinates */
    method void draw()
    {
        var int memAddress;
        let memAddress = 16384+location;
        do Memory.poke(memAddress+0, 896);
        do Memory.poke(memAddress+32, 640);
        do Memory.poke(memAddress+64, 640);
        do Memory.poke(memAddress+96, 640);
        do Memory.poke(memAddress+128, 4080);
        do Memory.poke(memAddress+160, 4104);
        do Memory.poke(memAddress+192, 32766);
        do Memory.poke(memAddress+224, -32767);
        do Memory.poke(memAddress+256, -32767);
        do Memory.poke(memAddress+288, 32766);
        do Memory.poke(memAddress+320, 17442);
        do Memory.poke(memAddress+352, 15324);
        return;
    }

    /** Erases tank from the screen
      * used in both destroying and moving the tank */
    method void erase()
    {
        var int memAddress;
        let memAddress = 16384+location;
        do Memory.poke(memAddress+0, 0);
        do Memory.poke(memAddress+32, 0);
        do Memory.poke(memAddress+64, 0);
        do Memory.poke(memAddress+96, 0);
        do Memory.poke(memAddress+128, 0);
        do Memory.poke(memAddress+160, 0);
        do Memory.poke(memAddress+192, 0);
        do Memory.poke(memAddress+224, 0);
        do Memory.poke(memAddress+256, 0);
        do Memory.poke(memAddress+288, 0);
        do Memory.poke(memAddress+320, 0);
        do Memory.poke(memAddress+352, 0);
        return;
    }

    /** Erases and disposes the tank
      * only used when the player loses */
    method void destroy()
    {
        do erase();
        do dispose();
        return;
    }

    /** Moves tank left or right depending on
      * which key is currently pressed */
    method void move()
    {
        do changeDirection();
        do checkWallCollision();
        do erase();
        let x = x + (direction * 16);
        do updateLocation();
        do draw();
        return;
    }
    
    /** Checks for collision on walls 
      * changing direction when necessary */
    method void checkWallCollision() 
    {
        if (direction = 1) 
        {
            if ((x + WIDTH) > 511) 
            {
                do stop();
            }
        }
        else 
        {
            if ((x - WIDTH) < 0) 
            {
                do stop();
            }
        }
        return;
    }
    
    /** Changes direction */
    method void changeDirection()
    {
        var char currentKey;
        let currentKey = Keyboard.keyPressed();
    
        if (currentKey = 132)
        {
            let direction = 1;
        }
        else
        {
            if (currentKey = 130)
            {
               let direction = -1;
            }
        }
        return;
    }

    /** Updates the memory address location */
    method void updateLocation()
    {
        let location = (32 * y) + (x / 16);
        return;
    }

    /** Stops Tank movement */
    method void stop()
    {
        let direction = 0;
        return;
    }

    /** Instantiate a Bullet and fire it straight up */
    method Bullet fire()
    {
        var Bullet bullet;
        let bullet = Bullet.new(x, y);
        return bullet;
    }

}